Attribute VB_Name = "CSI"
Option Explicit
Option Base 0


Public Function CSIgetExpiration(strFilenameFull As String) As Double

'-------------------------------------------------------------------
'
'Desc:      Opens the specified CSI ASCII output file and reads the last value from the delivery month column.
'           Use this to know what is the actual futures contract for a modified contract.
'
'Inputs:    fileNameFull - The full directory and filename of the ASCII file generated by CSI
'
'Output:    A double number in the form "YYYYMM" that represents the actual delivery month
'-------------------------------------------------------------------

Dim oFileSystem As Scripting.FileSystemObject
Dim oFile As Scripting.TextStream
Dim dataLine As String, headerLine As String

Dim colStart As Integer, colEnd As Integer, colCount As Integer, colFound As Boolean
Dim colName As String, colLen As Integer, colCurrent As Integer
    
    'Set up the objects
    Set oFileSystem = New Scripting.FileSystemObject
    Set oFile = oFileSystem.OpenTextFile(strFilenameFull, ForReading, False)
    
    'Read the header line
    headerLine = oFile.ReadLine
    
    'Read the last line of the file
    Do While oFile.AtEndOfStream <> True
        dataLine = oFile.ReadLine
    Loop
        
    'Find the location of "Numeric Delivery Month"
    colName = "Numeric Delivery Month"
    colLen = 6
    
    colStart = 1
    colEnd = 1
    colCount = 0
    colFound = False
    
    Do Until (colFound = True)
        colEnd = InStr(colStart, headerLine, ",", vbTextCompare)
        If colEnd = 0 Then colEnd = Len(headerLine) + 1
        
        colCount = colCount + 1
        
        If Trim$(Mid$(headerLine, colStart, colEnd - colStart)) = colName Then
            colFound = True
            End If
        colStart = colEnd + 1
    Loop
            
    colStart = 1
    For colCurrent = 1 To (colCount - 1)
        colStart = InStr(colStart, dataLine, ",", vbTextCompare) + 1
    Next

    CSIgetExpiration = Mid$(dataLine, colStart, colLen)
End Function

